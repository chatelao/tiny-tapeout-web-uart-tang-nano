/*
 * Multiply accumulate systolic array top of size NxN
 *
 * Julia Desmazes, 2025, this code is human made
 */

`timescale 1ns / 1ps

module mac #(
        parameter IO_W = 8,
        parameter W = 8, // data and weight width
        parameter N = 2, // matrix dimention
        parameter UREG_ADDR = 4
)(
        input wire clk,
        input wire rst_n,
        input wire ena,

        input wire            data_v_i,
        input wire [1:0]      data_mode_i,
        input wire [IO_W-1:0] data_i,

        /* DFT: JTAG USER_REG */
        input wire  [UREG_ADDR-1:0] jtag_ureg_addr_i,
        output wire [W-1:0]         jtag_ureg_data_o,

        output wire            result_v_o,
        output wire [IO_W-1:0] result_o
);
localparam NN = N*N;
genvar x,y;

/* FSM */
logic [NN-1:0] wr_weight_v_flat; // limited support for multidimentional array in the simulator
logic          wr_weight_v[N-1:0][N-1:0];
logic [N-1:0]  wr_data_v;
reg   [N*W-1:0] data_input_q;
logic          mac_step;
logic [2:0]   rd_res_seq_v;

mac_fsm #(.IO_W(IO_W), .W(W)) m_fsm(
        .clk(clk),
        .rst_n(rst_n),
        .ena(ena),

        .data_v_i(data_v_i),
        .data_mode_i(data_mode_i),

        .wr_weight_v_o(wr_weight_v_flat),
        .wr_data_v_o(wr_data_v),

        .mac_step_o(mac_step),

        .rd_res_seq_v_o(rd_res_seq_v)
);
generate
        for(x=0; x<N; x=x+1) begin: g_wr_weight_v_x
                for(y=0; y<N; y=y+1) begin: g_wr_weight_v_y
                        assign wr_weight_v[x][y] = wr_weight_v_flat[y*N+x];
                end
        end
endgenerate

/* Steamin data */
mac_streamin #(.IN_W(IO_W), .W(W)) m_mac_data_streamin_2x2(
        .clk(clk),
        .data_i(data_i),
        .data_wr_v_i(wr_data_v),
        .data_o(data_input_q)
);

/* Systolic array */

logic [W-1:0] data_unit[N-1:0][N-1:0];
logic [W-1:0] data_flow_right[N-1:0][N-1:0];
logic [W-1:0] data_top_unit[N-1:0][N-1:0];
logic [W-1:0] res_unit[N-1:0][N-1:0];

logic [W-1:0] jtag_ureg_data[NN-1:0];

generate
        for(y=0; y<N; y=y+1) begin: g_data_unit
                assign data_unit[0][y] = data_input_q[y*W+:W];
                for(x=1; x<N; x=x+1) begin: g_data_unit_flow
                        assign data_unit[x][y] = data_flow_right[x-1][y];
                end
        end

        /* data top */
        for(x=0; x < N; x=x+1) begin: g_data_top_x
                assign data_top_unit[x][0] = {W{1'b0}};
                for(y=1; y < N; y=y+1) begin: g_data_top_y
                        assign data_top_unit[x][y] = res_unit[x][y-1];
                end
        end

        for(x=0; x < N; x=x+1) begin: g_unit_x
                for(y=0; y < N; y=y+1) begin: g_unit_y
                        mac_unit #(.W(W)) m_unit(
                                .clk(clk),

                                .step_i(mac_step),

                                .data_i(data_unit[x][y]),
                                .data_top_i(data_top_unit[x][y]),

                                .wr_weight_v_i(wr_weight_v[x][y]),
                                .weight_i(data_i),

                                .jtag_ureg_addr_i(jtag_ureg_addr_i[1:0]),
                                .jtag_ureg_data_o(jtag_ureg_data[y*N+x]),

                                .data_o(data_flow_right[x][y]),
                                .res_o(res_unit[x][y])
                        );
                end
        end
endgenerate
wire [W-1:0] debug_res0_0, debug_res1_0, debug_res0_1, debug_res1_1;
assign debug_res0_0 = res_unit[0][0];
assign debug_res1_0 = res_unit[1][0];
assign debug_res0_1 = res_unit[0][N-1];
assign debug_res1_1 = res_unit[1][N-1];

/* result streamout */
mac_streamout #(.W(W), .OUT_W(IO_W)) m_mac_result_streamout_2x2(
        .clk(clk),
        .rst_n(rst_n),
        .res_rd_seq_v_i(rd_res_seq_v),
        .res_data_i({res_unit[1][N-1], res_unit[0][N-1]}),
        .valid_o(result_v_o),
        .data_o(result_o)
);

// JTAG user register access
assign jtag_ureg_data_o = jtag_ureg_data[jtag_ureg_addr_i[3:2]];

endmodule
